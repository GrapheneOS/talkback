// For building the open-source release of TalkBack.

apply plugin: 'com.android.application'

apply from: 'shared.gradle'

dependencies {
    implementation project(':talkback')
}

buildscript {
    ext.kotlin_version = "1.8.0"
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'org.aspectj:aspectjtools:1.8.1'
        // Here we set the version of the Android Gradle Plugin used by this
        // script. It is best to use as new a version as possible, but beware
        // that each version of the Gradle Plugin requires that the version of
        // Gradle (used to drive the build) be a certain version or above. The
        // correspondence is tracked here:
        // https://developer.android.com/build/releases/gradle-plugin#updating-gradle
        classpath 'com.android.tools.build:gradle:8.13.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

android {
    buildToolsVersion '36.1.0'
    defaultConfig {
        applicationId talkbackApplicationId
        versionName "15.1"
        versionCode 21
        minSdk 26
        targetSdk 35
        compileSdk {
            version = release(36) {
                it.minorApiLevel = 1
            }
        }
        testInstrumentationRunner 'android.test.InstrumentationTestRunner'
        multiDexEnabled true
    }
    packagingOptions {
        exclude 'LICENSE'
        exclude 'README'
    }
    lint {
        checkReleaseBuilds false
        abortOnError false
    }
    productFlavors {
        phone {
            dimension "target"
        }
        wear {
            dimension "target"
            versionNameSuffix "-wear"
        }
    }
}
